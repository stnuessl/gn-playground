#
# The MIT License (MIT)
#
# Copyright (c) 2026  Steffen Nuessle
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
#

import("//gn/python/python.gni")
import("//gn/vars.gni")

template("sources") {
  assert(defined(invoker.outputs), "'outputs' must be specified")

  python(target_name) {
    _ignore = [
      "include_sources",
      "exclude_sources",
      "include_sources_with_metadata",
      "exclude_sources_with_metadata",
    ]

    forward_variables_from(invoker, "*", _ignore)

    deps = [ metadata_target ]
    inputs = metadata_outputs

    env =
        [ "PYTHONPATH=" + rebase_path("//gn/python/packages", root_build_dir) ]
    requirements = [ "xxhash" ]
    script = "//gn/sources/select-sources.py"
    args = [
      "--metadata",
      rebase_path(metadata_outputs[0], root_build_dir),
      "-o",
      rebase_path(outputs[0], root_build_dir),
    ]

    if (defined(invoker.include_sources)) {
      args += [ "--include-sources" ] + invoker.include_sources
    }

    if (defined(invoker.exclude_sources)) {
      args += [ "--exclude-sources" ] + invoker.exclude_sources
    }

    if (defined(invoker.include_sources_with_metadata)) {
      args += [ "--include-sources-with-metadata" ] +
              invoker.include_sources_with_metadata
    }

    if (defined(invoker.exclude_sources_with_metadata)) {
      args += [ "--exclude-sources-with-metadata" ] +
              invoker.exclude_sources_with_metadata
    }
  }
}
