#
# The MIT License (MIT)
#
# Copyright (c) 2026 Steffen Nuessle
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
#

import("//gn/python/python.gni")

template("action") {
  assert(defined(invoker.script), "'script' must be specified")

  if (defined(invoker.env)) {
    _target_type = "python"
    _script = "//gn/action/invoke-action.py"
    _args = [ invoker.script ]
    _inputs = [ invoker.script ]
  } else {
    _target_type = "action"
    _script = invoker.script
    _args = []
    _inputs = [ invoker.script ]
  }

  target(_target_type, target_name) {
    _ignore = [
      "args",
      "inputs",
      "requirements",
      "script",
    ]
    forward_variables_from(invoker, "*", _ignore)

    inputs = _inputs
    if (defined(invoker.inputs)) {
      inputs += invoker.inputs
    }

    script = _script

    args = _args
    if (defined(invoker.args)) {
      args += invoker.args
    }
  }
}
