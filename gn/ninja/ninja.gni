#
# The MIT License (MIT)
#
# Copyright (c) 2026 Steffen Nuessle
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
#

import("//gn/action/action.gni")

# Template for invoking ninja from within the main ninja build.
template("ninja") {
  assert(defined(invoker.build_dir), "'build_dir' must be specified")

  action(target_name) {
    _ignore = [
      "args",
      "build_dir",
      "jobs",
      "ninja_file",
      "targets",
    ]
    forward_variables_from(invoker, "*", _ignore)

    if (!defined(outputs)) {
      outputs = []
    }

    outputs += [
      "${invoker.build_dir}/.ninja_deps",
      "${invoker.build_dir}/.ninja_log",
    ]

    # The called ninja instance has to decide for itself which targets must be
    # build and which not.
    outputs += [ "$target_gen_dir/$target_name.non-existant" ]

    # Ninja spawns its own worker processes during the build. To prevent
    # excessive process creation and oversubscription, we limit the number of
    # ninja processes that can run in parallel within the build system.
    pool = "//:console"
    mnemonic = "NINJA"

    script = ninja_tool
    args = []

    if (defined(invoker.jobs)) {
      args += [ "-j${invoker.jobs}" ]
    }

    args += [
      "-C",
      rebase_path(invoker.build_dir, root_build_dir),
    ]

    if (defined(invoker.ninja_file)) {
      args += [
        "-f",
        rebase_path(invoker.ninja_file, root_build_dir),
      ]
    }

    if (defined(invoker.targets)) {
      args += invoker.targets
    }
  }
}
