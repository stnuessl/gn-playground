#
# The MIT License (MIT)
#
# Copyright (c) 2025-2026  Steffen Nuessle
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
#

import("//gn/ninja/ninja.gni")
import("//gn/python/python.gni")
import("//gn/toolchains/clang/vars.gni")
import("//gn/vars.gni")

action("gn-check") {
  testonly = true
  deps = [ ack_target ]
  inputs = ack_outputs
  outputs = [ "$target_gen_dir/$target_name.non-existant" ]
  mnemonic = "CHECK"

  script = gn_tool
  args = [
    "check",
    rebase_path(root_build_dir, root_build_dir),
  ]
}

python("make-build") {
  _checker_script = "//gn/includes/check-include-directories.py"

  testonly = true
  deps = [ description_target ]
  inputs = description_outputs + [ _checker_script ]
  outputs = [ "$target_gen_dir/build.ninja" ]

  env = [ "PYTHONPATH=" + rebase_path("//gn/python/packages", root_build_dir) ]
  requirements = [
    "ninja",
    "xxhash",
  ]
  script = "//gn/includes/make-build.py"
  args = [
    "--build-directory",
    rebase_path(target_gen_dir, root_build_dir),
    "-o",
    rebase_path(outputs[0], root_build_dir),
    "--description",
    rebase_path(description_outputs[0], root_build_dir),
    "--script",
    rebase_path(_checker_script, root_build_dir),
    "--compilation-database",
    rebase_path("$root_build_dir/compile_commands.json", root_build_dir),
    "--vfs-config",
    rebase_path(clang.vfs_config_path, root_build_dir),
  ]
}

ninja("unused-include-directories-check") {
  testonly = true
  deps = [ ":make-build" ]
  inputs = get_target_outputs(deps[0])

  env = [ "PYTHONPATH=" + rebase_path("//gn/python/packages", root_build_dir) ]
  build_dir = target_gen_dir
}

group("check") {
  testonly = true
  deps = [
    ":gn-check",
    ":unused-include-directories-check",
  ]
}
